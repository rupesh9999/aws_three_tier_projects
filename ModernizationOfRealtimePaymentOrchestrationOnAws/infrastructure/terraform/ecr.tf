#--------------------------------------------------------------
# ECR Repositories for Microservices
#--------------------------------------------------------------
locals {
  ecr_services = [
    "payment-initiation",
    "payment-execution",
    "payment-tracking",
    "payment-reconciliation",
    "payment-billing",
    "payment-risk",
    "payment-frontend"
  ]
}

resource "aws_ecr_repository" "services" {
  for_each = toset(local.ecr_services)

  name                 = "${var.project_name}-${each.value}"
  image_tag_mutability = "MUTABLE"
  force_delete         = true

  image_scanning_configuration {
    scan_on_push = true
  }

  encryption_configuration {
    encryption_type = "AES256"
  }

  tags = {
    Name    = "${var.project_name}-${each.value}"
    Service = each.value
  }
}

#--------------------------------------------------------------
# ECR Lifecycle Policy (keep last 10 images)
#--------------------------------------------------------------
resource "aws_ecr_lifecycle_policy" "services" {
  for_each   = aws_ecr_repository.services
  repository = each.value.name

  policy = jsonencode({
    rules = [{
      rulePriority = 1
      description  = "Keep last 10 images"
      selection = {
        tagStatus   = "any"
        countType   = "imageCountMoreThan"
        countNumber = 10
      }
      action = {
        type = "expire"
      }
    }]
  })
}
